buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.21"
    }
}
plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

def artifactVersion = '1.1.0'
def artifactGroup = 'com.icapps.build.gradle'
group "$artifactGroup"
version "$artifactVersion"

//plugins
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply plugin: 'idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from 'docs/README.txt'
}

repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://dl.bintray.com/nicolaverbeeck/maven" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.21"
    compile "org.jetbrains.kotlin:kotlin-reflect:1.3.21"

    compile "com.chimerapps:bitbucketcloud-api:0.1.2"
    compile "com.chimerapps.gradle:icapps-translations-plugin:1.0.5"
    compile "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.0.0-RC14"
    compile "de.felixschulze.gradle:gradle-hockeyapp-plugin:3.6"
    compile "com.github.triplet.gradle:play-publisher:1.2.0"
    compileOnly "com.android.tools.build:gradle:3.0.1"

    testCompile "junit:junit:4.11"
}

bintray {
    user = System.getenv('BINTRAY_USER_ICAPPS')
    key = System.getenv('BINTRAY_KEY_ICAPPS')
    pkg {
        repo = 'maven'
        name = 'icapps-build-gradle-plugin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/icapps/android_gradle_build'

        version {
            name = "$artifactVersion"
            desc = "Gradle plugin for icapps build"
            released = new Date()
        }
    }
    publications = ['maven']
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Koen Van Looveren"
            email "koen.vanlooveren@icapps.com"
            organization "icapps"
        }
        developer {
            name "Nicola Verbeeck"
            email "nicola.verbeeck@icapps.com"
            organization "icapps"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "$artifactGroup"
            artifactId 'plugin'
            version "$artifactVersion"

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Gradle plugin that will contain everything to build, check and deploy to the right platform (HockeyApp, Play Store)')
                root.appendNode('name', 'icapps-build-gradle-plugin')
                root.appendNode('url', 'https://github.com/icapps/android_gradle_build')
                root.children().last() + pomConfig
            }
        }
    }
}
